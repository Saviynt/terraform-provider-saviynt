---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "saviynt_workday_soap_connection_resource Resource - saviynt"
subcategory: ""
description: |-
  Create and manage Workday SOAP connector in Saviynt
---

# saviynt_workday_soap_connection_resource (Resource)

Create and manage Workday SOAP connector in Saviynt

## Example Usage

```terraform
// Copyright (c) 2025 Saviynt Inc.
// SPDX-License-Identifier: MPL-2.0

# Variables for sensitive and configurable values
variable "WORKDAY_USERNAME" {
  type        = string
  description = "Username for Workday SOAP connection"
}

variable "WORKDAY_PASSWORD" {
  type        = string
  description = "Password for Workday SOAP connection"
  sensitive   = true
}

variable "SOAP_ENDPOINT_URL" {
  type        = string
  description = "Workday SOAP endpoint URL"
}

variable "TENANT_NAME" {
  type        = string
  description = "Workday tenant name"
}

variable "VAULT_CONNECTION" {
  type        = string
  description = "Vault connection name for credential management"
  default     = ""
}

variable "VAULT_CONFIG" {
  type        = string
  description = "Vault configuration"
  default     = ""
}


resource "saviynt_workday_soap_connection_resource" "example" {
  # Required attributes
  connection_name = "Terraform_Workday_SOAP_Connector"

  # Optional base connector attributes
  description         = "Workday SOAP connection for HR data integration - ${var.TENANT_NAME}"
  default_sav_roles   = "ROLE_ADMIN"
  email_template      = "default_template"
  vault_connection    = var.VAULT_CONNECTION
  vault_configuration = var.VAULT_CONFIG
  save_in_vault       = var.VAULT_CONNECTION != "" ? "true" : "false"
  wo_version          = "v1.0"

  # Authentication - using variables for sensitive data
  username = var.WORKDAY_USERNAME
  password = var.WORKDAY_PASSWORD
  # Alternative: Use write-only password for enhanced security
  # password_wo  = var.WORKDAY_PASSWORD
  soap_endpoint = var.SOAP_ENDPOINT_URL

  # Data Import Configuration
  accounts_import_json = jsonencode({
    "call1" : {
      "call" : [
        {
          "name" : "GetWorkers",
          "connection" : "userAuth",
          "url" : var.SOAP_ENDPOINT_URL,
          "httpMethod" : "POST",
          "httpParams" : "{}",
          "httpHeaders" : {
            "Authorization" : "$${access_token}",
            "Content-Type" : "text/xml; charset=utf-8",
            "SOAPAction" : "urn:com.workday/bsvc/Human_Resources/v35.0#Get_Workers"
          },
          "httpContentType" : "text/xml",
          "successResponses" : {
            "statusCode" : [200, 201]
          },
          "requestBody" : trimspace(<<-EOF
            <soapenv:Envelope xmlns:soapenv="http://schemas.xmlsoap.org/soap/envelope/" xmlns:bsvc="urn:com.workday/bsvc">
              <soapenv:Header>
                <bsvc:Workday_Common_Header>
                  <bsvc:Include_Reference_Descriptors_In_Response>true</bsvc:Include_Reference_Descriptors_In_Response>
                </bsvc:Workday_Common_Header>
              </soapenv:Header>
              <soapenv:Body>
                <bsvc:Get_Workers_Request bsvc:version="v35.0">
                  <bsvc:Request_Criteria>
                    <bsvc:Exclude_Inactive_Workers>false</bsvc:Exclude_Inactive_Workers>
                    <bsvc:Exclude_Employees>false</bsvc:Exclude_Employees>
                    <bsvc:Exclude_Contingent_Workers>false</bsvc:Exclude_Contingent_Workers>
                  </bsvc:Request_Criteria>
                  <bsvc:Response_Filter>
                    <bsvc:Page>${PAGE_NUMBER}</bsvc:Page>
                    <bsvc:Count>${PAGE_SIZE}</bsvc:Count>
                  </bsvc:Response_Filter>
                  <bsvc:Response_Group>
                    <bsvc:Include_Reference>true</bsvc:Include_Reference>
                    <bsvc:Include_Personal_Information>true</bsvc:Include_Personal_Information>
                    <bsvc:Include_Employment_Information>true</bsvc:Include_Employment_Information>
                    <bsvc:Include_Organizations>true</bsvc:Include_Organizations>
                    <bsvc:Include_Roles>true</bsvc:Include_Roles>
                  </bsvc:Response_Group>
                </bsvc:Get_Workers_Request>
              </soapenv:Body>
            </soapenv:Envelope>
          EOF
          )
        }
      ]
    }
  })

  hr_import_json = jsonencode({
    "call1" : {
      "call" : [
        {
          "name" : "GetOrganizations",
          "connection" : "userAuth",
          "url" : var.SOAP_ENDPOINT_URL,
          "httpMethod" : "POST",
          "httpHeaders" : {
            "Content-Type" : "text/xml; charset=utf-8",
            "SOAPAction" : "urn:com.workday/bsvc/Human_Resources/v35.0#Get_Organizations"
          }
        }
      ]
    }
  })

  data_to_import = "Users,Accounts,Organizations"
  page_size      = "100"
  date_format    = "yyyy-MM-dd'T'HH:mm:ss.SSS'Z'"

  # Account Management with Workday-specific SOAP operations
  create_account_json = jsonencode({
    "accountIdPath" : "$.Response_Data.Worker.Worker_Reference.ID[?(@.type=='Employee_ID')].value",
    "processingType" : "SequentialAndIterative",
    "createAccountRequestPath" : "$.call1.call[0]",
    "successResponses" : {
      "statusCode" : [200, 201]
    }
  })

  update_account_json = jsonencode({
    "accountIdPath" : "$.Response_Data.Worker.Worker_Reference.ID[?(@.type=='Employee_ID')].value",
    "processingType" : "SequentialAndIterative",
    "updateAccountRequestPath" : "$.call1.call[0]",
    "successResponses" : {
      "statusCode" : [200, 201]
    }
  })

  delete_account_json = jsonencode({
    "accountIdPath" : "$.Response_Data.Worker.Worker_Reference.ID[?(@.type=='Employee_ID')].value",
    "processingType" : "SequentialAndIterative",
    "deleteAccountRequestPath" : "$.call1.call[0]"
  })

  enable_account_json = jsonencode({
    "accountIdPath" : "$.Response_Data.Worker.Worker_Reference.ID[?(@.type=='Employee_ID')].value",
    "processingType" : "SequentialAndIterative",
    "enableAccountRequestPath" : "$.call1.call[0]"
  })

  disable_account_json = jsonencode({
    "accountIdPath" : "$.Response_Data.Worker.Worker_Reference.ID[?(@.type=='Employee_ID')].value",
    "processingType" : "SequentialAndIterative",
    "disableAccountRequestPath" : "$.call1.call[0]"
  })

  # User Management
  update_user_json = jsonencode({
    "userIdPath" : "$.Response_Data.Worker.Worker_Reference.ID[?(@.type=='Employee_ID')].value",
    "processingType" : "SequentialAndIterative",
    "updateUserRequestPath" : "$.call1.call[0]"
  })

  modify_user_data_json = jsonencode({
    "userIdPath" : "$.Response_Data.Worker.Worker_Reference.ID[?(@.type=='Employee_ID')].value",
    "processingType" : "SequentialAndIterative",
    "modifyUserDataRequestPath" : "$.call1.call[0]"
  })

  # Access Management
  grant_access_json = jsonencode({
    "accountIdPath" : "$.Response_Data.Worker.Worker_Reference.ID[?(@.type=='Employee_ID')].value",
    "processingType" : "SequentialAndIterative",
    "grantAccessRequestPath" : "$.call1.call[0]"
  })

  revoke_access_json = jsonencode({
    "accountIdPath" : "$.Response_Data.Worker.Worker_Reference.ID[?(@.type=='Employee_ID')].value",
    "processingType" : "SequentialAndIterative",
    "revokeAccessRequestPath" : "$.call1.call[0]"
  })

  # Password Management
  change_pass_json = jsonencode({
    "accountIdPath" : "$.Response_Data.Worker.Worker_Reference.ID[?(@.type=='Employee_ID')].value",
    "processingType" : "SequentialAndIterative",
    "changePasswordRequestPath" : "$.call1.call[0]"
  })
  # Alternative: Use write-only for enhanced security
  # change_pass_json_wo = jsonencode({ ... })

  password_type          = "BASIC"
  password_min_length    = "8"
  password_max_length    = "20"
  password_noofcapsalpha = "1"
  password_noofdigits    = "1"
  password_noofsplchars  = "1"

  # Response Path Configuration for Workday SOAP responses
  responsepath_userlist     = "$.Response_Data.Worker"
  responsepath_pageresults  = "$.Response_Results.Page_Results"
  responsepath_totalresults = "$.Response_Results.Total_Results"

  # Advanced Configuration
  connection_json = jsonencode({
    "authType" : "basic",
    "url" : var.SOAP_ENDPOINT_URL,
    "httpMethod" : "POST",
    "httpHeaders" : {
      "Content-Type" : "text/xml; charset=utf-8"
    },
    "tenant" : var.TENANT_NAME,
    "timeout" : 30000,
    "retryCount" : 3
  })
  # Alternative: Use write-only for enhanced security
  # connection_json_wo = jsonencode({ ... })

  custom_config = jsonencode({
    "pagination" : {
      "enabled" : true,
      "pageSize" : 100,
      "maxPages" : 1000
    },
    "workdaySpecific" : {
      "tenant" : var.TENANT_NAME,
      "apiVersion" : "v35.0",
      "includeInactiveWorkers" : false
    },
    "errorHandling" : {
      "retryOnFailure" : true,
      "maxRetries" : 3,
      "retryDelay" : 5000
    }
  })

  pam_config = jsonencode({
    "Connection" : "Workday-SOAP",
    "ConnectorClass" : "sailpoint.connector.WorkdaySOAPConnector",
    "tenant" : var.TENANT_NAME,
    "apiVersion" : "v35.0"
  })

  combined_create_request = "true"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `connection_name` (String) Name of the connection. Example: "Active Directory_Doc"

### Optional

- `accounts_import_json` (String) JSON configuration for accounts import.
- `change_pass_json` (String, Sensitive) JSON configuration for password changes. Either this or change_pass_json_wo must be set.
- `change_pass_json_wo` (String) Write-only JSON configuration for password changes. Either this or change_pass_json must be set.
- `combined_create_request` (String) Combined create request configuration.
- `connection_json` (String, Sensitive) General connection JSON configuration. Either this or connection_json_wo must be set.
- `connection_json_wo` (String) Write-only general connection JSON configuration. Either this or connection_json must be set.
- `create_account_json` (String) JSON configuration for account creation.
- `custom_config` (String) Custom configuration JSON.
- `data_to_import` (String) Specification of data types to import. Example: "Users,Accounts"
- `date_format` (String) Date format for data processing. Example: "yyyy-MM-dd"
- `defaultsavroles` (String) Default SAV roles for managing the connection. Example: "ROLE_ORG"
- `delete_account_json` (String) JSON configuration for account deletion.
- `description` (String) Description for the connection. Example: "ORG_AD"
- `disable_account_json` (String) JSON configuration for account disabling.
- `email_template` (String) Email template for notifications. Example: "New Account Task Creation"
- `enable_account_json` (String) JSON configuration for account enabling.
- `grant_access_json` (String) JSON configuration for granting access.
- `hr_import_json` (String) JSON configuration for HR data import.
- `modify_user_data_json` (String) JSON configuration for modifying user data.
- `page_size` (String) Number of records per page. Example: "100"
- `pam_config` (String) PAM configuration JSON.
- `password` (String, Sensitive) Password for SOAP authentication. Either this or password_wo must be set.
- `password_max_length` (String) Maximum password length. Example: "20"
- `password_min_length` (String) Minimum password length. Example: "8"
- `password_noofcapsalpha` (String) Number of capital letters required in password. Example: "1"
- `password_noofdigits` (String) Number of digits required in password. Example: "1"
- `password_noofsplchars` (String) Number of special characters required in password. Example: "1"
- `password_type` (String) Type of password authentication. Example: "BASIC"
- `password_wo` (String) Write-only password for SOAP authentication. Either this or password must be set.
- `responsepath_pageresults` (String) Response path for page results.
- `responsepath_totalresults` (String) Response path for total results count.
- `responsepath_userlist` (String) Response path for user list.
- `revoke_access_json` (String) JSON configuration for revoking access.
- `save_in_vault` (String) Flag indicating whether the encrypted attribute should be saved in the configured vault. Example: "false"
- `soap_endpoint` (String) SOAP endpoint URL for Workday. Example: "https://wd2-impl-services1.workday.com/ccx/service/tenant/Human_Resources/v35.0"
- `update_account_json` (String) JSON configuration for account updates.
- `update_user_json` (String) JSON configuration for user updates.
- `username` (String) Username for SOAP authentication.
- `vault_configuration` (String) JSON string specifying vault configuration.
- `vault_connection` (String) Specifies the type of vault connection being used (e.g., 'Hashicorp'). Example: "Hashicorp"
- `wo_version` (String) Add/change the value of this attribute to update the writeonly attributes like username, password etc in connection resources

### Read-Only

- `connection_key` (Number) Unique identifier of the connection returned by the API. Example: 1909
- `error_code` (String) An error code where '0' signifies success and '1' signifies an unsuccessful operation.
- `id` (String) Resource ID.
- `msg` (String) A message indicating the outcome of the operation.
